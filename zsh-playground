#!/usr/bin/env zsh

'builtin' 'emulate' '-L' 'zsh' '-o' 'no_aliases' '-o' 'err_return' || 'builtin' 'exit'
setopt no_unset extended_glob typeset_silent no_multi_byte \
       prompt_percent no_prompt_subst warn_create_global pipe_fail

() {

if [[ ${ZSH_VERSION-} != (5.<8->*|<6->.*) ]]; then
  print -ru2 -- "${ZSH_SCRIPT:t}: zsh >= 5.8 is required to execute this script"
  return 1
fi

local -r root_dir=${ZSH_SCRIPT:A:h}

zmodload zsh/zutil zsh/system zsh/zselect

local -a help privileged

if [[ -r /proc/1/cpuset(#qN-.) &&
      "$(</proc/1/cpuset)" == /docker/[[:xdigit:]](#c64) ]]; then
  local -a isolation=(--isolation user)
  local -ri in_docker=1
else
  local -a isolation=(--isolation docker)
  local -ri in_docker=0
fi

zparseopts -D -K -F -- {h,-help}=help {p,-privileged}=privileged {I,-isolation}:=isolation

if (( $#help )); then
  print -r -- "usage: ${ZSH_SCRIPT:t} [OPTION].. CONFIG"
  print -r --
  print -r -- 'OPTIONS'
  print -r -- '  -h,--help'
  print -r -- '  -p,--privileged'
  print -r -- '  -I,--isolation <docker|user> [default=docker]'
  return
fi

if [[ $isolation[2] != (docker|user) ]]; then
  print -ru2 -- "${ZSH_SCRIPT:t}: invalid value of --isolation: ${(q-)isolation[2]}"
  return 1
fi

if (( ARGC > 1 )); then
  print -ru2 -- "${ZSH_SCRIPT:t}: too many positional arguments"
  return 1
fi

if (( ARGC )); then
  local -r cfg=$1

  if [[ -z $cfg ]]; then
    print -ru2 -- "${ZSH_SCRIPT:t}: config name cannot be empty"
    return 1
  fi

  if [[ ! -e $root_dir/configs/$cfg ]]; then
    print -ru2 -- "${ZSH_SCRIPT:t}: directory does not exist: $root_dir/configs/$cfg"
    return 1
  fi
else
  local -r cfg=
fi

if [[ ! -t 0 || ! -t 1 || ! -t 2 ]]; then
  print -ru2 -- "${ZSH_SCRIPT:t}: all standard file descriptors must be TTY"
  return 1
fi

if [[ $isolation[2] == user ]]; then
  if (( EUID )); then
    if (( in_docker )); then
      print -ru2 -- "${ZSH_SCRIPT:t}: you must be root for --isolation user"
    else
      print -ru2 -- "${ZSH_SCRIPT:t}: you must be root for --isolation user; try with sudo"
    fi
    return 1
  fi
  local -a create_repo
  [[ -v commands[git] ]] && create_repo=('~/zsh-bench/internal/create-repo ~/repo' 'cd ~/repo')
  exec -- $root_dir/internal/install-config \
    'zsh-playground-user' $#privileged "$cfg" $create_repo 'exec zsh -l'
  return
fi

[[ $isolation[2] == docker ]]

() {
  local cmd
  for cmd in docker tar; do
    if [[ ! -v commands[$cmd] ]]; then
      print -ru2 -- "${ZSH_SCRIPT:t}: command not found: ${(q-)cmd}"
      return 1
    fi
  done
}

local -r img=ubuntu:21.04

function docker-run() {
  local flags=(-i --rm --init -e TERM -e COLORTERM)
  if (( $#privileged )); then
    flags+=(--privileged -v /:/host)
  fi
  DOCKER_CONTENT_TRUST=1 command docker run $flags -- $img sh -uec "$1"
}

if ! docker-run true; then
  print -ru2 -- "${ZSH_SCRIPT:t}: cannot run docker; try with sudo?"
  return 1
fi

local self
self=${"$(cd -q -- $root_dir && command tar -cz -- ^.git && print -n x)"[1,-2]}

print -r -- "==> starting $img in docker ..."

local setup=(
  'cpuset="$(cat /proc/1/cpuset)"'
  '[ -n "$cpuset" ]'
  'printf "==> docker container ID: %.12s\\n" "${cpuset##*/}" >&2'
  'mkdir ~/zsh-bench'
  'cd ~/zsh-bench'
  'head -c '$#self' | tar -xzm'
  'cd /'
  '( ( cat <&3 >/dev/null || true; /bin/kill -TERM -- -$$; ) & ) 3<&0'
  '~/zsh-bench/internal/install-deps >&2'
  'zsh="$(which zsh)"'
  'chsh -s "$zsh"'
  '~/zsh-bench/configs/minimal/setup'
  'printf "%s\\n" "$cpuset"'
  'cat >/dev/null')

if [[ -n $cfg ]]; then
  local login=(
    'exec'
    '~/zsh-bench/internal/install-config'
    'zsh-playground-user'
    $#privileged
    ${(qqq)cfg}
    '"~/zsh-bench/internal/create-repo ~/repo"'
    '"cd ~/repo"'
    '"exec zsh -l"')
else
  local login=('cd && exec zsh -l')
fi

local tty
exec {tty}<&0

{
  exec {tty}<&-
  print -r -- $sysparams[pid]
  print -rn -- $self
  trap 'exit 0' TERM PIPE
  while true; do
    zselect -t 360000 || true
    print 2>/dev/null || exit 0
  done
} | {
  exec {tty}<&-
  local pid
  IFS= read -r pid
  print -r -- $pid
  docker-run ${(j: && :)setup}
} | {
  local pid
  IFS= read -r pid
  {
    local cpuset
    IFS= read -r cpuset
    exec 0<&$tty {tty}<&-
    if [[ $cpuset != /docker/[[:xdigit:]]## ]]; then
      print -ru2 -- "${ZSH_SCRIPT:t}: unexpected content of /proc/1/cpuset in the docker container"
      return 1
    fi
    local ret
    command docker exec -e TERM -e COLORTERM -it -- ${cpuset:t} sh -c ${(j: :)login} || ret=$?
    return ret
  } always {
    kill -- $pid 2>/dev/null || true
  }
}

} "$@"
