#!/usr/bin/env zsh

emulate -L zsh -o err_return
setopt no_unset typeset_silent no_multi_byte warn_create_global pipe_fail

() {

zmodload zsh/zutil

local -x LC_ALL=C

local -a help scratch_dir
zparseopts -D -K -F -- \
  {h,-help}=help       \
  {d,-scratch-dir}:=scratch_dir

if (( $#help )); then
  print -r -- "usage: ${ZSH_SCRIPT:t} [OPTION].. [-- [PASSTHROUGH]..]"
  print -r --
  print -r -- 'OPTIONS'
  print -r -- '  -h,--help'
  print -r -- '  -d,--scratch-dir <directory>'
  return
fi

if (( ! $#scratch_dir )); then
  print -ru2 -- "${ZSH_SCRIPT:t}: missing required flag: --scratch-dir"
  return 1
fi
if [[ ! -e $scratch_dir[2] ]]; then
  print -ru2 -- "${ZSH_SCRIPT:t}: directory does not exist: ${(q-)scratch_dir[2]}"
  return 1
fi

if [[ ! -t 0 || ! -t 1 || ! -t 2 ]]; then
  print -ru2 -- "${ZSH_SCRIPT:t}: all standard file descriptors must be TTY"
  return 1
fi
if [[ ! -v commands[stty] ]]; then
  print -ru2 -- "${ZSH_SCRIPT:t}: command not found: stty"
  return 1
fi
if [[ ! -v commands[reset] ]]; then
  print -ru2 -- "${ZSH_SCRIPT:t}: command not found: reset"
  return 1
fi
if [[ ! -v commands[scriptreplay] ]]; then
  print -ru2 -- "${ZSH_SCRIPT:t}: command not found: scriptreplay"
  return 1
fi

unset _zb_stty
typeset -g _zb_stty
_zb_stty=$(command stty -g)

function cleanup() {
  local key
  while true; do
    [[ -t 2 ]] || read -t0 -k key || break
  done 2>/dev/null
  command reset
  command stty $_zb_stty
  [[ $1 == EXIT ]] && exit
  exit $((127 + ${signals[(Ie)$1]}))
}

local sig trapped=(${${(A)=:-INT TERM HUP EXIT}:*signals})
for sig in $trapped; do
  trap "trap - $trapped; cleanup $sig" $sig
done
unset sig trapped

local -r clear=${(pl:$LINES::\n:):-}$'\e[H'

print -rn -- $clear
print -r -- '1. Press ENTER to see the replay of the TTY content.'
print -r -- '2. Press ENTER again at the end to exit.'

local REPLY
IFS= read -rs
print -rn -- $clear

print -rn -- $clear
command scriptreplay "$@" -t $scratch_dir[2]/timing -- $scratch_dir[2]/out
IFS= read -rs

} "$@"
